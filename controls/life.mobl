module controls::life

import ui

import models::lifeevents
import mobl::notify

function loadPresets() : [(Bool, String, Num, [String])] {
  var presets : [(Bool, String, Num, [String])] = Array<(Bool, String, Num, [String])>();
  presets.push((false, "Holidays", 12, ["confidence", "forgiveness", "joy", "relationship", "gratitude", "heart", "love", "power", "affirmations", "anxiety", "breathing"]));
  presets.push((false, "Parents visiting", 11, ["anxiety", "mindfulness", "loosen", "obsessive thinking"]));
  presets.push((false, "Many family get-togethers", 15, ["family", "confidence", "forgiveness", "joy", "relationship", "gratitude", "heart", "love", "power", "affirmations"]));
  presets.push((false, "Vacation", 15, ["spiritual", "awakening", "loosen", "meditation", "relax", "detox", "peace", "stillness", "balance", "cleansing"]));
  presets.push((false, "Change in eating habits", 15, ["food", "nourishment", "excersice", "detox", "yoga", "balance", "body", "chakra", "wellness", "immunity", "immune"]));
  presets.push((false, "Change in sleeping habits", 16, ["sleep", "relaxation", "anxiety", "breathing", "detox", "yoga", "balance", "body", "chakra", "wellness", "peace", "meditation"]));
  return presets;
}

var eventsToSave : [LifeEvent] = Array<LifeEvent>();

control lifeForm(presets : [(Bool, String, Num, [String])]) {
  block(main) {
    list((check, name, stress, keywords) in presets) {
    	var event_name = name.replace(/\s/g, "__")
      customItem {
      	<span id="item__" + event_name>
        checkBox(check, name, onchange={
          if (check) {
            $("#item__" + event_name).css("font-weight", "bold");
            var lifeEvent = LifeEvent(name=event_name, stress=stress, date=now(), keywords=keywords);
            eventsToSave.push(lifeEvent);
          } else {
            $("#item__" + event_name).css("font-weight", "normal");
          	var eventToRemove : LifeEvent = null;
          	foreach (event in eventsToSave) {
          		if (event.name == event_name) {
          			eventToRemove = event;
          		}
          	}
          	eventsToSave.remove(eventToRemove);
          }
        })
        </span>
      }
    }
  }
  var saveButtonImage = "resources/images/save.png"
  block(action) {
    <span onmousedown={
      saveButtonImage = "resources/images/save_pressed.png";
    } onmouseup={
      saveButtonImage = "resources/images/save.png";
    }>
    image(saveButtonImage, onclick={
      $("#flash__Life").show();
      foreach (event in eventsToSave) {
        add(event);
      }
      eventsToSave.length = 0;
    })
    </span>
  }
}

control life() {
  lifeForm(loadPresets())
}
